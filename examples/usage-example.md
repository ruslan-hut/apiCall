## Пример использования АПИ в 1С 7.7
# Вызов из формы
Вызов из формы, например, для загрузки заказов из интернет-магазина
```CODE
Процедура ЗагрузитьССайта_АПИ()
    Перем тбЗаказы, РабочийКаталог, Рез;
    
    // рабочий каталог в котором будут созданы файлы с полученными данными
    РабочийКаталог = "C:\temp\";
              
    // получение данных о заказах
	Рез = АПИ_ВыполнитьМетод(РабочийКаталог, "unload-orders","GET",,тбЗаказы);   
	
	Если Рез<>1 Тогда
		Сообщить("Данные о заказах не получены","!");
		Возврат;
	КонецЕсли;  

    // Загрузка заказов из таблицы тбЗаказы
    
КонецПроцедуры
```

# Вызов утилиты
функция получения данных из АПИ с использованием внешней утилиты call.exe
```CODE
Функция АПИ_ВыполнитьМетод(РабочийКаталог, Ресурс, Метод, ДанныеДляОтправки="", ДанныеПолученные="")   
	
	Если ПустаяСтрока(РабочийКаталогАПИ)=1 Тогда
		Сообщить("Не выполнено подключение","!");
		Возврат 0;
	КонецЕсли;
	
	Если ТипЗначения(ДанныеДляОтправки)=100 Тогда
		Если АПИ_ПодготовитьДанныеДляОтправки(РабочийКаталог, ДанныеДляОтправки)=0 Тогда
			Сообщить("Не удалось сформировать данные для отправки","!");
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	АпиХелпер 	= РабочийКаталогАПИ+"call.exe";  
	Конфиг		= РабочийКаталогАПИ+"config.yml";
	Ошибки 		= РабочийКаталог+"errors.log";  // лог во временном каталоге
	
	СтрокаЗапуска = ""+АпиХелпер+" -url="+Ресурс+" -method="+Метод+" -conf="+Конфиг+" -path="+РабочийКаталог;
	            
	// запуск с отображением консоли
	Если ДоступнаКомпонента=0 Тогда
		
		Попытка   
			КомандаСистемы(СтрокаЗапуска); 
		Исключение    
			Сообщить(СтрокаЗапуска);
			Сообщить("failed to run: "+ОписаниеОшибки(),"!");
			Возврат 0;
		КонецПопытки; 
		
	КонецЕсли;
	          
    // обработка ошибок, полученных из лога утилиты                           
	флОшибкиВызова = 0;
	Если ФС.СуществуетФайл(Ошибки)=1 Тогда
		Текст = СоздатьОбъект("Текст");
		Текст.Открыть(Ошибки);
		Для Н=1 По Текст.КоличествоСтрок() Цикл
			С = Текст.ПолучитьСтроку(Н);
			Если Найти(С, "#Error:") > 0 Тогда
				флОшибкиВызова = 1;
				ТекстОшибки = СтрЗаменить(С, "#Error:", ""); 
				Сообщить(ТекстОшибки);
			КонецЕсли; 
			Если Найти(С, "#Warn:") > 0 Тогда
				ТекстОшибки = СтрЗаменить(С, "#Warn:", ""); 
				Сообщить(ТекстОшибки);
			КонецЕсли;
		КонецЦикла;  
	Иначе
		Сообщить("log file not found");
	КонецЕсли;                     
	
	Если флОшибкиВызова=1 Тогда 
		Возврат 0;
	КонецЕсли;  
	
	Если АПИ_ПрочитатьДанные(РабочийКаталог, ДанныеПолученные)=0 Тогда  
		Сообщить(""+Метод+": "+Ресурс+" got empty response");
		Возврат -1;
	КонецЕсли;  
	
	Возврат 1;
	
КонецФункции   
```

# Дополнительные функции
Для полноценной работы также необходимы функции для записи данных в файл и чтения данных из файла

Запись таблицы значений в файл CSV. Формирует файл CSV из таблицы значений, при этом элементы, содержащие кавычки или запятые, экранируются кавычками
```CODE
Функция АПИ_ПодготовитьДанныеДляОтправки(РабочийКаталог, Данные)   
	
	К = Данные.КоличествоСтрок();
	
	Если К=0 Тогда
		Возврат 0;
	КонецЕсли;   
	
	Текст = СоздатьОбъект("Текст");  
	Текст.КодоваяСтраница(0);
	
	Заголовки = "";
	СтрокаДанных = "";
	      
	Для Н=1 По Данные.КоличествоКолонок() Цикл                        
		Имя = Данные.ПолучитьПараметрыКолонки(Н);
		Заголовки = ""+Заголовки+?(Н=1,"",",")+Имя; 
	КонецЦикла;   
	Текст.ДобавитьСтроку(Заголовки);  
	
	Для Н=1 По К Цикл 
		СтрокаДанных = "";
		
		Для НН=1 По Данные.КоличествоКолонок() Цикл                        
			
			Элемент = Данные.ПолучитьЗначение(Н,НН);   
			
			Если Найти(Элемент, """")>0 Тогда
				Элемент = СтрЗаменить(Элемент, """", """""");
			КонецЕсли;
			Если (Найти(Элемент, ",")>0) ИЛИ (Найти(Элемент, """")>0) Тогда                 
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+""""+Элемент+"""";
			Иначе
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+Элемент;
			КонецЕсли;
			
		КонецЦикла;   
		
		Текст.ДобавитьСтроку(СтрокаДанных);
	КонецЦикла;
	
	// чтобы отправить один объект, передается таблица с одой строкой
	Текст.Записать(""+РабочийКаталог+?(К=1,"object","input")+".csv");
	
	Возврат 1;
	
КонецФункции
```

Полный набор функций доступен в файле [example-full-set.md](example-full-set.md)
