```CODE
//=================== Работа с REST API ======================
//

// ===============================
Процедура Журнал(Статус,Содержание)

	Сообщить(ТекущееВремя()+" "+Содержание, Статус);
	
	//глЗаписьЖурналаОбслуживания(ИДОбработки,Статус,Содержание);

КонецПроцедуры

// ===============================
// формирует список значений из строки файла CSV
Функция АПИ_Парсить(С)

	С = СтрЗаменить(С, """""", "'");
	С = СтрЗаменить(С, "<nil>", "");
	
	Разделитель = ",";
	Р = СоздатьОбъект("СписокЗначений");
	
	НачалсяЭлемент = 0;                           
	ТекущийЭлемент = "";
	
	Для Н=1 По СтрДлина(С) Цикл
		
		Б = Сред(С, Н, 1);        
		
		Если Б<>Разделитель Тогда 
			
			Если НачалсяЭлемент=0 Тогда              
				
				Если Б="""" Тогда
					НачалсяЭлемент = 1;
				Иначе
					ТекущийЭлемент = ?(СтрДлина(ТекущийЭлемент)=100,ТекущийЭлемент,""+ТекущийЭлемент+Б); 
				КонецЕсли;
				
			Иначе   
				
				Если Б="""" Тогда
					Если Н=СтрДлина(С) Тогда
						// последний символ в строке   
						Р.ДобавитьЗначение(СокрЛП(ТекущийЭлемент));
						НачалсяЭлемент = 0;         
						ТекущийЭлемент = "";  
					Иначе
						Дальше = Сред(С, Н+1, 1);
						Если Дальше=Разделитель Тогда
							// кавычка перед разделителем
							Р.ДобавитьЗначение(СокрЛП(ТекущийЭлемент));
							НачалсяЭлемент = 0;         
							ТекущийЭлемент = "";
							Н = Н+1;
						КонецЕсли;
					КонецЕсли;
				Иначе
					ТекущийЭлемент = ?(СтрДлина(ТекущийЭлемент)=100,ТекущийЭлемент,""+ТекущийЭлемент+Б);
				КонецЕсли;
				
			КонецЕсли;  
			
		Иначе 
			
			Если НачалсяЭлемент=1 Тогда
				ТекущийЭлемент = ?(СтрДлина(ТекущийЭлемент)=100,ТекущийЭлемент,""+ТекущийЭлемент+Б); 
			Иначе
				Р.ДобавитьЗначение(СокрЛП(ТекущийЭлемент));
				ТекущийЭлемент = "";
			КонецЕсли;  
			
		КонецЕсли;
		
	КонецЦикла;  
	                
	Р.ДобавитьЗначение(СокрЛП(ТекущийЭлемент));
	
	Возврат Р;

КонецФункции

// ===============================    
// формирует файл CSV из таблицы значений
// элементы, содержащие кавычки или запятые, экранируются кавычками
Функция АПИ_ПодготовитьДанныеДляОтправкиТаблица(РабочийКаталог, Данные)

	К = Данные.КоличествоСтрок();
	
	Если К=0 Тогда
		Возврат 0;
	КонецЕсли;   
	
	Текст = СоздатьОбъект("Текст");  
	Текст.КодоваяСтраница(0);
	
	Заголовки = "";
	СтрокаДанных = "";
	      
	Для Н=1 По Данные.КоличествоКолонок() Цикл                        
		Имя = Данные.ПолучитьПараметрыКолонки(Н);
		Заголовки = ""+Заголовки+?(Н=1,"",",")+Имя; 
	КонецЦикла;   
	Текст.ДобавитьСтроку(Заголовки);  
	
	Для Н=1 По К Цикл 
		СтрокаДанных = "";
		
		Для НН=1 По Данные.КоличествоКолонок() Цикл                        
			
			Элемент = Данные.ПолучитьЗначение(Н,НН);   
			
			Если Найти(Элемент, """")>0 Тогда
				Элемент = СтрЗаменить(Элемент, """", """""");
			КонецЕсли;
			Если (Найти(Элемент, ",")>0) ИЛИ (Найти(Элемент, """")>0) Тогда                 
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+""""+Элемент+"""";
			Иначе
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+Элемент;
			КонецЕсли;
			
		КонецЦикла;   
		
		Текст.ДобавитьСтроку(СтрокаДанных);
	КонецЦикла;
	
	// чтобы отправить один объект, передается таблица с одой строкой
	Текст.Записать(""+РабочийКаталог+?(К=1,"object","input")+".csv");
	
	Возврат 1;

КонецФункции

// ===============================    
// формирует файл CSV из таблицы значений
// элементы, содержащие кавычки или запятые, экранируются кавычками
Функция АПИ_ПодготовитьТаблицуДляОтправки(РабочийКаталог, ИмяТаблицы, Данные) Экспорт

	К = Данные.КоличествоСтрок();
	
	Если К=0 Тогда
		Возврат 0;
	КонецЕсли;  
	
	Текст = СоздатьОбъект("Текст");  
	Текст.КодоваяСтраница(0);
	
	ИмяФайла = ""+РабочийКаталог+"input_"+ИмяТаблицы+".csv";
	
	Если ФС.СуществуетФайл(ИмяФайла)=1 Тогда
		Текст.Открыть(ИмяФайла);
	КонецЕсли;
	
	Заголовки = "";
	СтрокаДанных = "";
	
	Если Текст.КоличествоСтрок()=0 Тогда
		Для Н=1 По Данные.КоличествоКолонок() Цикл                        
			Имя = Данные.ПолучитьПараметрыКолонки(Н);
			Заголовки = ""+Заголовки+?(Н=1,"",",")+Имя; 
		КонецЦикла;   
		Текст.ДобавитьСтроку(Заголовки);  
	КонецЕсли;
	
	Для Н=1 По К Цикл 
		СтрокаДанных = "";
		
		Для НН=1 По Данные.КоличествоКолонок() Цикл                        
			
			Элемент = СокрЛП(Данные.ПолучитьЗначение(Н,НН));   
			
			Если Найти(Элемент, """")>0 Тогда
				Элемент = СтрЗаменить(Элемент, """", """""");
			КонецЕсли;
			Если (Найти(Элемент, ",")>0) ИЛИ (Найти(Элемент, """")>0) Тогда                 
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+""""+Элемент+"""";
			Иначе
				СтрокаДанных = ""+СтрокаДанных+?(НН=1,"",",")+Элемент;
			КонецЕсли;
			
		КонецЦикла;   
		
		Текст.ДобавитьСтроку(СтрокаДанных);
	КонецЦикла;
	          
	Попытка
		Текст.Записать(ИмяФайла);  
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка записи файла: "+ОписаниеОшибки(),"OpenCart",,,5); 
		Возврат 0;
	КонецПопытки;
	
	Данные.УдалитьСтроки();
	ЗаписьЖурналаРегистрации("Подготовлена таблица данных: "+ИмяФайла+"; строк: "+Текст.КоличествоСтрок(),"OpenCart");
	
	Возврат 1;

КонецФункции

// ===============================  
// читает содержимое CSV файла в таблицу значений
// в первой строке файла считываются имена полей
// каждая строка должна содержать количество элементов равное количеству заголовков полей
Функция АПИ_ДобавитьФайлВТаблицу(РабочийКаталог, Т, ИмяФайла)

	Если ПустаяСтрока(ИмяФайла)=1 Тогда
		Возврат 0;
	КонецЕсли;
	
	ПолноеИмя = ""+РабочийКаталог+ИмяФайла;
	Если ФС.СуществуетФайл(ПолноеИмя)=0 Тогда 
		Возврат 0;
	КонецЕсли;
	
	Текст = СоздатьОбъект("Текст");
	Текст.Открыть(ПолноеИмя); 
	
	Строк = Текст.КоличествоСтрок();
	Если Строк<2 Тогда  
		//Сообщить(""+ПолноеИмя+" (пустой)");
		Возврат 0;
	КонецЕсли; 
	
	Заголовки = АПИ_Парсить(Текст.ПолучитьСтроку(1));  
	
	Если Т.КоличествоКолонок()=0 Тогда   
		Для Н=1 По Заголовки.РазмерСписка() Цикл
			Имя = Заголовки.ПолучитьЗначение(Н);
			Т.НоваяКолонка(Имя);
		КонецЦикла; 
	КонецЕсли;    
	
	Если Заголовки.РазмерСписка()<>Т.КоличествоКолонок() Тогда
		Сообщить(""+ПолноеИмя+": количество полей в файле не корректно ("+Заголовки.РазмерСписка()+")");
		Возврат 0;
	КонецЕсли;
	
	Для Н=2 По Строк Цикл    
		
		СтрокаДанных = Текст.ПолучитьСтроку(Н);
		Если ПустаяСтрока(СтрокаДанных)=1 Тогда 
			//Сообщить("Пустая строка "+Н);
			Продолжить;
		КонецЕсли;     
		
		Элементы = АПИ_Парсить(СтрокаДанных);
		Если Элементы.РазмерСписка()<>Т.КоличествоКолонок() Тогда
			Журнал("!", "Количество элементов не корректно ("+Элементы.РазмерСписка()+"); строка "+Н);
			//Сообщить("> "+СтрокаДанных);
			//Для К=1 По Элементы.РазмерСписка() Цикл
			//	Элемент = Элементы.ПолучитьЗначение(К);
			//	Сообщить("> "+К+": "+Элемент);
			//КонецЦикла;
			Продолжить;
		КонецЕсли; 
		
		Т.НоваяСтрока();
		Состояние(""+ПолноеИмя+": прочитано строк: "+Т.КоличествоСтрок());
		
		Для К=1 По Элементы.РазмерСписка() Цикл 
			
			Колонка = Заголовки.ПолучитьЗначение(К);
			Элемент = Элементы.ПолучитьЗначение(К);  
			
			Попытка 
				Т.УстановитьЗначение(Т.КоличествоСтрок(), Колонка, Элемент);
			Исключение  
				Журнал("!", ""+Колонка+"="+Элемент+"; "+ОписаниеОшибки());
			КонецПопытки;
			
		КонецЦикла;
	КонецЦикла;  
	
	Возврат 1;
КонецФункции

// ===============================
Функция АПИ_ПрочитатьДанные(РабочийКаталог, Т)              
ФайлыДанных = РабочийКаталог+"output*.csv";
Т = СоздатьОбъект("ТаблицаЗначений");
ИмяФайла = ФС.НайтиПервыйФайл(ФайлыДанных);
Пока АПИ_ДобавитьФайлВТаблицу(РабочийКаталог, Т, ИмяФайла)=1 Цикл
ИмяФайла = ФС.НайтиСледующийФайл();
КонецЦикла;
Возврат Т.КоличествоСтрок();
КонецФункции

// ===============================
Функция АПИ_ВыполнитьМетод(РабочийКаталог, Ресурс, Метод, ДанныеДляОтправки="", ДанныеПолученные="") Экспорт

	Если ПустаяСтрока(РабочийКаталогАПИ)=1 Тогда
		Журнал("!", "Не выполнено подключение");
		Возврат 0;
	КонецЕсли;
	
	Если ТипЗначения(ДанныеДляОтправки)=100 Тогда
		Если АПИ_ПодготовитьДанныеДляОтправкиТаблица(РабочийКаталог, ДанныеДляОтправки)=0 Тогда
			Сообщить("Не удалось сформировать данные для отправки","!");
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	АпиХелпер 	= РабочийКаталогАПИ+"call.exe";  
	Конфиг		= РабочийКаталогАПИ+"config.yml";
	Ошибки 		= РабочийКаталог+"errors.log";  // лог во временном каталоге
	
	СтрокаЗапуска = ""+АпиХелпер+" -url="+Ресурс+" -method="+Метод+" -conf="+Конфиг+" -path="+РабочийКаталог;
	
	//Журнал(,СтрокаЗапуска);     
	      
	// запуск командной строки без вывода окна консоли
	путьКдлл = строка(КаталогПрограммы()+"DialMail.dll");
	ДоступнаКомпонента = ЗагрузитьВнешнююКомпоненту(путьКдлл); 
	Если ДоступнаКомпонента=1 Тогда      
		Сис = СоздатьОбъект("AddIn.SystComm");
		Сис.ЗапуститьОжидая(СтрокаЗапуска); 
	КонецЕсли;
	            
	// обычный запуск с отображением консоли
	Если ДоступнаКомпонента=0 Тогда
		
		Попытка   
			КомандаСистемы(СтрокаЗапуска); 
		Исключение    
			Журнал("!",""+СтрокаЗапуска);
			Журнал("!!!","failed to run: "+ОписаниеОшибки());
			Возврат 0;
		КонецПопытки; 
		
	КонецЕсли;
	                                     
	флОшибкиВызова = 0;
	Если ФС.СуществуетФайл(Ошибки)=1 Тогда
		Текст = СоздатьОбъект("Текст");
		Текст.Открыть(Ошибки);
		Для Н=1 По Текст.КоличествоСтрок() Цикл
			С = Текст.ПолучитьСтроку(Н);
			Если Найти(С, "#Error:") > 0 Тогда
				флОшибкиВызова = 1;
				ТекстОшибки = СтрЗаменить(С, "#Error:", ""); 
				Журнал("!!", ТекстОшибки); 
				ЗаписьЖурналаРегистрации(ТекстОшибки,"OpenCart","Response", ,5);
			КонецЕсли; 
			//Если Найти(С, "#Warn:") > 0 Тогда
			//	ТекстОшибки = СтрЗаменить(С, "#Warn:", ""); 
			//	Журнал("!", ТекстОшибки);
			//КонецЕсли;
		КонецЦикла;  
	Иначе
		Журнал("!","log file not found");
	КонецЕсли;                     
	
	Если флОшибкиВызова=1 Тогда 
		//Журнал("!!!", "failed "+Метод+": "+Ресурс);
		Возврат 0;
	КонецЕсли;  
	
	Если АПИ_ПрочитатьДанные(РабочийКаталог, ДанныеПолученные)=0 Тогда  
		//Журнал("",""+Метод+": "+Ресурс+" got empty response");
		Возврат -1;
	КонецЕсли;  
	
	Возврат 1;

КонецФункции

// ===============================
Функция АПИ_ОтправитьТаблицуДанных(РабочийКаталог, Путь, Метод, тбЗапрос, Комментарий="") Экспорт

	Если тбЗапрос.КоличествоСтрок()=0 Тогда
		Возврат 1;                                                             
	КонецЕсли;
	
	Если Метод="POST" Тогда
		Операция = "Добавление";
	ИначеЕсли Метод="PUT" Тогда
		Операция = "Обновление";
	ИначеЕсли Метод="DELETE" Тогда
		Операция = "Удаление";
	Иначе
		Журнал("!", "Неизвестный метод "+Метод);
		Возврат 0;
	КонецЕсли;
	
	//Журнал(, ""+Операция+": "+Комментарий+": "+тбЗапрос.КоличествоСтрок());
	
	врТаблица = СоздатьОбъект("ТаблицаЗначений");
	НаСтраницу = 500; 
	
	К = Цел(тбЗапрос.КоличествоСтрок()/НаСтраницу);
	
	Для Н=1 По К Цикл      
		
		тбЗапрос.Выгрузить(врТаблица, НаСтраницу*(Н-1)+1, Н*НаСтраницу); 
		
		Если АПИ_ВыполнитьМетод(РабочийКаталог, Путь, Метод, врТаблица, )=0 Тогда  
			Возврат 0;
		КонецЕсли;
		
	КонецЦикла; 
	
	Если К*НаСтраницу < тбЗапрос.КоличествоСтрок() Тогда  
		
		тбЗапрос.Выгрузить(врТаблица, К*НаСтраницу+1); 
		
		Если АПИ_ВыполнитьМетод(РабочийКаталог, Путь, Метод, врТаблица, )=0 Тогда  
			Возврат 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат 1;

КонецФункции

// ===============================  
Функция АПИ_ИмяРабочегоКаталога(Корень)

	Путь = "tmp_"+СтрЗаменить(ТекущаяДата(),".","");  
	Путь = ""+Путь+"_"+СтрЗаменить(ТекущееВремя(),":",""); 
	
	Возврат ""+Корень+Путь;

КонецФункции

// ===============================  
// создает временный каталог для создания файлов и возвращает путь к нему
Функция АПИ_ПодготовитьРабочийКаталог()

	Путь = АПИ_ИмяРабочегоКаталога(РабочийКаталогАПИ);  
	
	Если ФС.СуществуетФайл(Путь)=0 Тогда
		ФС.СоздатьКаталог(Путь);
	Иначе  
		
		Инд=1;
		НовыйПуть = ""+Путь+"_"+Инд;
		
		Пока ФС.СуществуетФайл(НовыйПуть)=1 Цикл
			Инд = Инд+1;
			НовыйПуть = ""+Путь+"_"+Инд;
		КонецЦикла;    
		
		Путь = НовыйПуть;
		ФС.СоздатьКаталог(Путь);
		
		//Маска = ""+Путь+"input*.csv";
		//ИмяФайла = ФС.НайтиПервыйФайл(Маска);
		//Пока ФС.СуществуетФайл(""+Путь+ИмяФайла)=1 Цикл
		//	ФС.УдалитьФайл(""+Путь+ИмяФайла);
		//	ИмяФайла = ФС.НайтиСледующийФайл(); 
		//	Если ПустаяСтрока(ИмяФайла)=1 Тогда
		//		Прервать;
		//	КонецЕсли;
		//КонецЦикла;  
		//ИмяФайла = ""+Путь+"object.csv";
		//Если ФС.СуществуетФайл(ИмяФайла)=1 Тогда
		//	ФС.УдалитьФайл(ИмяФайла);
		//КонецЕсли; 
	КонецЕсли;  
	
	Путь = ""+Путь+"\";
	Возврат Путь;
КонецФункции

// ===============================  
// возвращает путь к каталогу для временных файлов
Функция АПИ_Подключить(Сайт, ТолькоПроверка=0) Экспорт

	РабочийКаталогАПИ = СокрЛП(Сайт.РабочийКаталог); 
	
	АпиХелпер = ""+РабочийКаталогАПИ+"call.exe";
	
	Если ФС.СуществуетФайл(АпиХелпер)=0 Тогда    
		РабочийКаталогАПИ = "";
		Журнал("!", "Не настроен рабочий каталог для сайта "+Сайт);
		Возврат "";
	КонецЕсли;   
	
	Возврат ?(ТолькоПроверка=1, "+", АПИ_ПодготовитьРабочийКаталог());

КонецФункции

// ===============================
Функция АПИ_Отключить(Путь="") Экспорт
Перем Сис;
ДоступнаКомпонента=0;

	Если ПустаяСтрока(Путь)=0 Тогда  
		// удаление всех временных каталогов кроме сегодняшних  
		
		//путьКдлл = строка(КаталогПрограммы()+"DialMail.dll");
		//ДоступнаКомпонента = ЗагрузитьВнешнююКомпоненту(путьКдлл); 
		//Если ДоступнаКомпонента=1 Тогда      
		//	Сис = СоздатьОбъект("AddIn.SystComm");
		//КонецЕсли;
		
		Маска = ""+РабочийКаталогАПИ+"tmp_*";
		ИмяФайла = ""+РабочийКаталогАПИ+ФС.НайтиПервыйФайл(Маска);  
		
		Сегодня = "tmp_"+СтрЗаменить(ТекущаяДата(), ".", "");
		
		Пока ФС.СуществуетФайл(ИмяФайла)=1 Цикл  
			Если Найти(ИмяФайла, Сегодня)=0 Тогда 
				//Сообщить("Удаление временных файлов: "+ИмяФайла);
				ЗаписьЖурналаРегистрации("Удаление временных файлов: "+ИмяФайла,"OpenCart");
				Попытка     
					Команда = "rd /q /s "+ИмяФайла;
					//Сообщить(Команда);       
					Если ДоступнаКомпонента=1 Тогда
						Сис.ЗапуститьОжидая(Команда);
					Иначе
						КомандаСистемы(Команда);
					КонецЕсли;
				Исключение
					ЗаписьЖурналаРегистрации(ОписаниеОшибки(),"OpenCart",,,5);
				КонецПопытки; 
			КонецЕсли;
			Файл = ФС.НайтиСледующийФайл();
			ИмяФайла = ""+РабочийКаталогАПИ+Файл; 
			Если ПустаяСтрока(Файл)=1 Тогда
				Прервать;
			КонецЕсли; 
		КонецЦикла;  
		            
		// удаление текущего рабочего каталога
		Если ФС.СуществуетФайл(Путь)=1 Тогда
			Попытка     
				Команда = "rd /q /s "+Путь;
				КомандаСистемы(Команда);
				//Журнал("","Временные файлы удалены");
			Исключение
				Журнал("!","Ошибка удаления временных файлов: "+ОписаниеОшибки());
			КонецПопытки; 
		КонецЕсли;
		
	КонецЕсли;
	
	РабочийКаталогАПИ = ""; 
	Путь = "";
		
	Возврат 1;

КонецФункции

// ===============================
Функция АПИ_ЕстьРабочееПодключение() Экспорт

	Сайты = СоздатьОбъект("Справочник.Сайты");
	Если Сайты.ВыбратьЭлементы()=0 Тогда
		Журнал("!", "Не заданы настройки обмена с сайтами");
		Возврат 0;
	КонецЕсли;  
	
	флЕстьРабочее = 0;
	Сайты.ВыбратьЭлементы();
	Пока Сайты.ПолучитьЭлемент()=1 Цикл 
		
		Если Сайты.ПометкаУдаления()=1 Тогда
			Продолжить;
		КонецЕсли;   
		
		Сайт = Сайты.ТекущийЭлемент();
		
		Если АПИ_Подключить(Сайт, 1)="" Тогда
			Журнал("!", ""+Сайт+": подключение не установлено");
			Продолжить;
		КонецЕсли;
		АПИ_Отключить();
		
		флЕстьРабочее = 1;
		
	КонецЦикла;
	
	Возврат флЕстьРабочее;

КонецФункции

//
//=================== Работа с REST API ======================
```